! (C) Copyright 1989- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

! This file is a copy of ifsaux/module/grib_interface.F90
! Any changes there should also be made here. This is to avoid
! a dependency of wam on ifsaux
!
MODULE YOWGRIB

!define GRIB_API_TRACE

!**** Interface to GRIB_API

!     Purpose.
!     --------
!     Fortran 90 Interface to calling GRIB API

!     Author.
!     -------
!        M.Hamrud     ECMWF

!     Modifications.
!     --------------
!        Original: 2005-11-15
!      F. Vana  05-Mar-2015  Support for single precision

!     ------------------------------------------------------------------

USE PARKIND1 , ONLY : JPRD, JPIM, JPIB, JPRM
USE YOMHOOK  , ONLY : LHOOK, DR_HOOK, JPHOOK
USE GRIB_API
USE MPL_MODULE, ONLY : MPL_ABORT
IMPLICIT NONE

INTERFACE IGRIB_GET_VALUE
MODULE PROCEDURE &
 & IGRIB_GET_INT, IGRIB_GET_REAL4, IGRIB_GET_REAL8,IGRIB_GET_CHAR,&
 & IGRIB_GET_INT_ARRAY, IGRIB_GET_REAL4_ARRAY, IGRIB_GET_REAL8_ARRAY, &
 & IGRIB_GET_INT8
END INTERFACE
INTERFACE IGRIB_SET_VALUE
MODULE PROCEDURE &
 & IGRIB_SET_INT, IGRIB_SET_REAL4, IGRIB_SET_REAL8,IGRIB_SET_CHAR,&
 & IGRIB_SET_INT_ARRAY, IGRIB_SET_REAL4_ARRAY, IGRIB_SET_REAL8_ARRAY, &
 & IGRIB_SET_INT8, IGRIB_SET_INT8_ARRAY
END INTERFACE
INTERFACE IGRIB_READ_BYTES
MODULE PROCEDURE &
 & IGRIB_READ_BYTES_INT, IGRIB_READ_BYTES_REAL4, IGRIB_READ_BYTES_REAL8, IGRIB_READ_BYTES_CHAR
END INTERFACE
INTERFACE IGRIB_WRITE_BYTES
MODULE PROCEDURE &
 & IGRIB_WRITE_BYTES_INT, IGRIB_WRITE_BYTES_REAL4, IGRIB_WRITE_BYTES_REAL8, IGRIB_WRITE_BYTES_CHAR
END INTERFACE

PUBLIC IGRIB_GET_VALUE, IGRIB_SET_VALUE, IGRIB_OPEN_FILE, IGRIB_CLOSE_FILE,&
 & IGRIB_RELEASE, JPGRIB_SUCCESS, JPGRIB_END_OF_FILE, JPGRIB_BUFFER_TOO_SMALL, &
 & IGRIB_GET_MESSAGE_SIZE, IGRIB_NEW_FROM_MESSAGE, IGRIB_NEW_FROM_FILE, &
 & IGRIB_NEW_FROM_SAMPLES, IGRIB_CLONE ,IGRIB_GET_MESSAGE, IGRIB_READ_FROM_FILE, &
 & IGRIB_READ_BYTES, IGRIB_WRITE_BYTES, IGRIB_WRITE, IGRIB_IS_DEFINED, IGRIB_ERROR_MESSAGE

INTEGER, PARAMETER :: JPGRIB_SUCCESS=GRIB_SUCCESS
INTEGER, PARAMETER :: JPGRIB_END_OF_FILE=GRIB_END_OF_FILE
INTEGER, PARAMETER :: JPGRIB_BUFFER_TOO_SMALL=GRIB_BUFFER_TOO_SMALL

PRIVATE
#ifdef GRIB_API_1
INTEGER, PARAMETER, PUBLIC :: JPKSIZE_T=JPIM
#else
INTEGER, PARAMETER, PUBLIC :: JPKSIZE_T=KINDOFSIZE_T
#endif

LOGICAL, PUBLIC :: LGRIB_API_TRACE = .FALSE.

CONTAINS

SUBROUTINE IGRIB_GET_INT8(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIB),INTENT(OUT) :: KVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT8',0,ZHOOK_HANDLE)
CALL GRIB_GET_LONG(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_LONG',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT8',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_INT8

SUBROUTINE IGRIB_GET_INT(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIM),INTENT(OUT) :: KVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT',0,ZHOOK_HANDLE)
CALL GRIB_GET_INT(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_INT',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_INT

SUBROUTINE IGRIB_GET_REAL4(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRM)   ,INTENT(OUT) :: PVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL4',0,ZHOOK_HANDLE)
CALL GRIB_GET_REAL4(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_REAL4',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL4',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_REAL4

SUBROUTINE IGRIB_GET_REAL8(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRD)   ,INTENT(OUT) :: PVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL8',0,ZHOOK_HANDLE)
CALL GRIB_GET_REAL8(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_REAL8',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL8',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_REAL8

SUBROUTINE IGRIB_GET_CHAR(KHANDLE,CDKEY,CDVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
CHARACTER(LEN=*)  ,INTENT(OUT) :: CDVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET
INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_CHAR',0,ZHOOK_HANDLE)
CALL GRIB_GET_STRING(KHANDLE,CDKEY,CDVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_STRING',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_CHAR',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_CHAR

SUBROUTINE IGRIB_GET_INT_ARRAY(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIM),INTENT(OUT) :: KVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET,ISIZE
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT_ARRAY',0,ZHOOK_HANDLE)

CALL GRIB_GET_INT_ARRAY(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_INT',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_INT_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_INT_ARRAY

SUBROUTINE IGRIB_GET_REAL4_ARRAY(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRM)   ,INTENT(OUT) :: PVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL4_ARRAY',0,ZHOOK_HANDLE)

CALL GRIB_GET_REAL4_ARRAY(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_REAL4_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL4_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_REAL4_ARRAY

SUBROUTINE IGRIB_GET_REAL8_ARRAY(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRD)   ,INTENT(OUT) :: PVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL8_ARRAY',0,ZHOOK_HANDLE)

CALL GRIB_GET_REAL8_ARRAY(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_REAL8_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_GET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_REAL8_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_REAL8_ARRAY
!====================================================================
SUBROUTINE IGRIB_SET_INT8(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIB),INTENT(IN) :: KVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT8',0,ZHOOK_HANDLE)

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) WRITE (0, *) 'CALL GRIB_SET_LONG (IGRIBH, ', '"'//TRIM (CDKEY)//'"', ', ', KVAL, ')'
#endif

CALL GRIB_SET_LONG(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_LONG',KHANDLE,' ',CDKEY,' ',KVAL,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT8',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_INT8

SUBROUTINE IGRIB_SET_INT(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIM),INTENT(IN) :: KVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT',0,ZHOOK_HANDLE)

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) WRITE (0, *) 'CALL GRIB_SET_INT (IGRIBH, ', '"'//TRIM (CDKEY)//'"', ', ', KVAL, ')'
#endif

CALL GRIB_SET_INT(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_INT',KHANDLE,' ',CDKEY,' ',KVAL,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_INT

SUBROUTINE IGRIB_SET_REAL4(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRM)   ,INTENT(IN) :: PVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL4',0,ZHOOK_HANDLE)
CALL GRIB_SET_REAL4(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_REAL4',KHANDLE,' ',CDKEY,' ',PVAL,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL4',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_REAL4

SUBROUTINE IGRIB_SET_REAL8(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRD)   ,INTENT(IN) :: PVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) WRITE (0, *) 'CALL GRIB_SET_REAL8 (IGRIBH, ', '"'//TRIM (CDKEY)//'"', ', ', PVAL, ')'
#endif

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL8',0,ZHOOK_HANDLE)
CALL GRIB_SET_REAL8(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_REAL8',KHANDLE,' ',CDKEY,' ',PVAL,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL8',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_REAL8

SUBROUTINE IGRIB_SET_CHAR(KHANDLE,CDKEY,CDVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
CHARACTER(LEN=*)  ,INTENT(IN) :: CDVAL
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_CHAR',0,ZHOOK_HANDLE)

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) WRITE (0, *) 'CALL GRIB_SET_STRING (IGRIBH, ', '"'//TRIM (CDKEY)//'"', ', ', '"'//TRIM (CDVAL)//'"', ')'
#endif

CALL GRIB_SET_STRING(KHANDLE,CDKEY,CDVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_STRING',KHANDLE,' ',CDKEY,' ',CDVAL,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_CHAR',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_CHAR

SUBROUTINE IGRIB_SET_INT8_ARRAY(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIB),INTENT(IN) :: KVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
INTEGER(KIND=JPIM) :: I
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT8_ARRAY',0,ZHOOK_HANDLE)

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) &
& WRITE (0, *) 'CALL GRIB_SET_LONG_ARRAY (IGRIBH, ', '"'//TRIM (CDKEY)//'"', &
& ', (/ ', KVAL (1), ((', ', KVAL (I)), I = 2, SIZE (KVAL)), ' /))'
#endif

CALL GRIB_SET_LONG_ARRAY(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_LONG_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT8_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_INT8_ARRAY

SUBROUTINE IGRIB_SET_INT_ARRAY(KHANDLE,CDKEY,KVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
INTEGER(KIND=JPIM),INTENT(IN) :: KVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
INTEGER(KIND=JPIM) :: I
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT_ARRAY',0,ZHOOK_HANDLE)

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) &
& WRITE (0, *) 'CALL GRIB_SET_INT_ARRAY (IGRIBH, ', '"'//TRIM (CDKEY)//'"', &
& ', (/ ', KVAL (1), ((', ', KVAL (I)), I = 2, SIZE (KVAL)), ' /))'
#endif

CALL GRIB_SET_INT_ARRAY(KHANDLE,CDKEY,KVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_INT_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_INT_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_INT_ARRAY

SUBROUTINE IGRIB_SET_REAL4_ARRAY(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRM)   ,INTENT(IN) :: PVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL4_ARRAY',0,ZHOOK_HANDLE)
CALL GRIB_SET_REAL4_ARRAY(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_REAL4_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL4_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_REAL4_ARRAY

SUBROUTINE IGRIB_SET_REAL8_ARRAY(KHANDLE,CDKEY,PVAL,KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE
CHARACTER(LEN=*)  ,INTENT(IN) :: CDKEY
REAL(KIND=JPRD)   ,INTENT(IN) :: PVAL(:)
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
INTEGER(KIND=JPIM) :: I
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

#ifdef GRIB_API_TRACE
IF (LGRIB_API_TRACE) &
& WRITE (0, *) 'CALL GRIB_SET_REAL8_ARRAY (IGRIBH, ', '"'//TRIM (CDKEY)//'"', &
& ', (/ ', PVAL (1), ((', ', PVAL (I)), I = 2, SIZE (PVAL)), '/))'
#endif

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL8_ARRAY',0,ZHOOK_HANDLE)
CALL GRIB_SET_REAL8_ARRAY(KHANDLE,CDKEY,PVAL,STATUS=IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_SET_REAL8_ARRAY',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_SET_VALUE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_SET_REAL8_ARRAY',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_SET_REAL8_ARRAY

SUBROUTINE IGRIB_OPEN_FILE(KFILE,CDPATH,CDMODE)
INTEGER(KIND=JPIM),INTENT(OUT)  :: KFILE
CHARACTER(LEN=*)  ,INTENT(IN)   :: CDPATH
CHARACTER(LEN=1)  ,INTENT(IN)   :: CDMODE

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_OPEN_FILE',0,ZHOOK_HANDLE)
CALL GRIB_OPEN_FILE(KFILE,CDPATH,CDMODE,STATUS=IRET)
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_OPEN_FILE ',TRIM(CDPATH),' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_OPEN_FILE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_OPEN_FILE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_OPEN_FILE

SUBROUTINE IGRIB_CLOSE_FILE(KFILE)
INTEGER(KIND=JPIM),INTENT(IN)  :: KFILE

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_CLOSE_FILE',0,ZHOOK_HANDLE)
CALL GRIB_CLOSE_FILE(KFILE,STATUS=IRET)
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_CLOSE_FILE ',KFILE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_CLOSE_FILE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_CLOSE_FILE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_CLOSE_FILE

SUBROUTINE IGRIB_READ_FROM_FILE(KFILE,KBUF,KBYTES,KRET)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
INTEGER(KIND=JPIM),INTENT(OUT)   :: KBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_FROM_FILE',0,ZHOOK_HANDLE)

CALL GRIB_READ_FROM_FILE(KFILE,KBUF,KBYTES,IRET)
IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_READ_FROM_FILE ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_READ_FROM_FILE FAILED')
ENDIF

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_FROM_FILE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_READ_FROM_FILE

SUBROUTINE IGRIB_READ_BYTES_INT(KFILE,KBUF,KBYTES,KRET)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
INTEGER(KIND=JPIM),INTENT(OUT)   :: KBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM),INTENT(OUT)   :: KRET
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_INT',0,ZHOOK_HANDLE)
CALL GRIB_READ_BYTES(KFILE,KBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_READ_BYTES_INT ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_READ_BYTES_INT FAILED')
ENDIF
KRET = IRET
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_INT',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_READ_BYTES_INT

SUBROUTINE IGRIB_READ_BYTES_REAL4(KFILE,PBUF,KBYTES,KRET)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
REAL(KIND=JPRM)   ,INTENT(OUT)   :: PBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM),INTENT(OUT)   :: KRET
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_REAL4',0,ZHOOK_HANDLE)

CALL GRIB_READ_BYTES(KFILE,PBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_READ_BYTES_REAL4 ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_READ_BYTES_REAL4 FAILED')
ENDIF
KRET = IRET
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_REAL4',1,ZHOOK_HANDLE)
END SUBROUTINE IGRIB_READ_BYTES_REAL4

SUBROUTINE IGRIB_READ_BYTES_REAL8(KFILE,PBUF,KBYTES,KRET)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
REAL(KIND=JPRD)   ,INTENT(OUT)   :: PBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM),INTENT(OUT)   :: KRET
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_REAL8',0,ZHOOK_HANDLE)

CALL GRIB_READ_BYTES(KFILE,PBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_READ_BYTES_REAL8 ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_READ_BYTES_REAL8 FAILED')
ENDIF
KRET = IRET
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_REAL8',1,ZHOOK_HANDLE)
END SUBROUTINE IGRIB_READ_BYTES_REAL8

SUBROUTINE IGRIB_READ_BYTES_CHAR(KFILE,CDBUF,KBYTES,KRET)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
CHARACTER(LEN=1)  ,INTENT(OUT)   :: CDBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM),INTENT(OUT)   :: KRET
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_CHAR',0,ZHOOK_HANDLE)

CALL GRIB_READ_BYTES(KFILE,CDBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_READ_BYTES_CHAR ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_READ_BYTES_CHAR FAILED')
ENDIF
KRET = IRET
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_READ_BYTES_CHAR',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_READ_BYTES_CHAR


SUBROUTINE IGRIB_WRITE_BYTES_INT(KFILE,KBUF,KBYTES)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
INTEGER(KIND=JPIM),INTENT(IN)    :: KBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_INT',0,ZHOOK_HANDLE)

CALL GRIB_WRITE_BYTES(KFILE,KBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS ) THEN
  WRITE(0,*) 'GRIB_WRITE_BYTES_INT ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_WRITE_BYTES_INT FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_INT',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_WRITE_BYTES_INT

SUBROUTINE IGRIB_WRITE_BYTES_REAL4(KFILE,PBUF,KBYTES)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
REAL(KIND=JPRM)   ,INTENT(IN)    :: PBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_REAL4',0,ZHOOK_HANDLE)

CALL GRIB_WRITE_BYTES(KFILE,PBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS ) THEN
  WRITE(0,*) 'GRIB_WRITE_BYTES_REAL4 ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_WRITE_BYTES_REAL4 FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_REAL4',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_WRITE_BYTES_REAL4

SUBROUTINE IGRIB_WRITE_BYTES_REAL8(KFILE,PBUF,KBYTES)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
REAL(KIND=JPRD)   ,INTENT(IN)    :: PBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM) :: IRET,ILEN
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_REAL8',0,ZHOOK_HANDLE)

CALL GRIB_WRITE_BYTES(KFILE,PBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS ) THEN
  WRITE(0,*) 'GRIB_WRITE_BYTES_REAL8 ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_WRITE_BYTES_REAL8 FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_REAL8',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_WRITE_BYTES_REAL8

SUBROUTINE IGRIB_WRITE_BYTES_CHAR(KFILE,CDBUF,KBYTES)
INTEGER(KIND=JPIM),INTENT(IN)    :: KFILE
CHARACTER(LEN=1)  ,INTENT(IN)    :: CDBUF(:)
INTEGER(KIND=JPKSIZE_T),INTENT(INOUT) :: KBYTES
INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_CHAR',0,ZHOOK_HANDLE)

CALL GRIB_WRITE_BYTES(KFILE,CDBUF,KBYTES,IRET)
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_WRITE_BYTES_CHAR ',KFILE,' BUFFER SIZE ',KBYTES,' BYTES, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_WRITE_BYTES_CHAR FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE_BYTES_CHAR',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_WRITE_BYTES_CHAR

SUBROUTINE IGRIB_NEW_FROM_FILE(KFILE,KHANDLE,KRET)
INTEGER(KIND=JPIM),INTENT(IN)  :: KFILE
INTEGER(KIND=JPIM),INTENT(OUT) :: KHANDLE
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_FILE',0,ZHOOK_HANDLE)

CALL GRIB_NEW_FROM_FILE(KFILE,KHANDLE,STATUS=IRET)

IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_NEW_FROM_FILE ',KFILE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_NEW_FROM_FILE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_FILE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_NEW_FROM_FILE

SUBROUTINE IGRIB_WRITE(KFILE,KHANDLE,KRET)
INTEGER(KIND=JPIM),INTENT(IN)  :: KFILE
INTEGER(KIND=JPIM),INTENT(IN)  :: KHANDLE
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE',0,ZHOOK_HANDLE)

CALL GRIB_WRITE(KHANDLE,KFILE,STATUS=IRET)

IF(PRESENT(KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS .AND. IRET /= JPGRIB_END_OF_FILE ) THEN
  WRITE(0,*) 'GRIB_WRITE ',KFILE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_WRITE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_WRITE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_WRITE

SUBROUTINE IGRIB_NEW_FROM_MESSAGE(KHANDLE,KBUF)
INTEGER(KIND=JPIM),INTENT(OUT) :: KHANDLE
INTEGER(KIND=JPIM),INTENT(IN)  :: KBUF(:)

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_MESSAGE',0,ZHOOK_HANDLE)

CALL GRIB_NEW_FROM_MESSAGE(KHANDLE,KBUF,STATUS=IRET)

IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'CALL TO GRIB_NEW_FROM_MESSAGE FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('IGRIB_NEW_FROM_MESSAGE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_MESSAGE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_NEW_FROM_MESSAGE

SUBROUTINE IGRIB_RELEASE(KHANDLE)
INTEGER(KIND=JPIM),INTENT(IN) :: KHANDLE

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_RELEASE',0,ZHOOK_HANDLE)

CALL GRIB_RELEASE(KHANDLE,STATUS=IRET)
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_RELEASE ',KHANDLE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_RELEASE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_RELEASE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_RELEASE

SUBROUTINE IGRIB_GET_MESSAGE_SIZE(KHANDLE,KBYTES)
INTEGER(KIND=JPIM),INTENT(IN)  :: KHANDLE
INTEGER(KIND=JPKSIZE_T),INTENT(OUT) :: KBYTES
INTEGER(KIND=JPIM) :: IRET
INTEGER(KIND=KINDOFSIZE_T) :: IBYTES
REAL(KIND=JPHOOK)    :: ZHOOK_HANDLE


IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_MESSAGE_SIZE',0,ZHOOK_HANDLE)
CALL GRIB_GET_MESSAGE_SIZE(KHANDLE,IBYTES,STATUS=IRET)
KBYTES = IBYTES
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_GET_MESSAGE_SIZE ',KHANDLE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT(' GRIB_GET_MESSAGE_SIZE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_MESSAGE_SIZE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_MESSAGE_SIZE

SUBROUTINE IGRIB_GET_MESSAGE(KHANDLE,KGRIB)
INTEGER(KIND=JPIM),INTENT(IN)  :: KHANDLE
INTEGER(KIND=JPIM),INTENT(OUT) :: KGRIB(:)

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
CHARACTER(LEN=1),ALLOCATABLE   :: CLS(:)
INTEGER(KIND=JPIM) :: ILENINT
INTEGER(KIND=JPKSIZE_T) :: ILEN

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_MESSAGE',0,ZHOOK_HANDLE)

!TEMP CODING
CALL IGRIB_GET_MESSAGE_SIZE(KHANDLE,ILEN)
ILENINT=(ILEN+3)/4
ALLOCATE(CLS(ILENINT*4))
CLS(ILENINT*4-3:ILENINT*4)='    '
CALL GRIB_COPY_MESSAGE(KHANDLE,CLS,STATUS=IRET)
KGRIB(1:ILENINT)=TRANSFER(CLS,KGRIB)
DEALLOCATE(CLS)
!CALL GRIB_COPY_MESSAGE(KHANDLE,KGRIB,STATUS=IRET)
IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_COPY_MESSAGE ',KHANDLE,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT(' GRIB_COPY_MESSAGE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_GET_MESSAGE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_GET_MESSAGE

SUBROUTINE IGRIB_NEW_FROM_SAMPLES(KHANDLE,CDNAME,KRET)
INTEGER(KIND=JPIM),INTENT(OUT) :: KHANDLE
CHARACTER(LEN=*),INTENT(IN) :: CDNAME
INTEGER(KIND=JPIM),OPTIONAL,INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_SAMPLES',0,ZHOOK_HANDLE)
CALL GRIB_NEW_FROM_SAMPLES(KHANDLE,TRIM(CDNAME),STATUS=IRET)
IF (PRESENT (KRET)) THEN
  KRET = IRET
ELSEIF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_NEW_FROM_SAMPLES ',TRIM(CDNAME),' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT(' GRIB_NEW_FROM_SAMPLES FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_NEW_FROM_SAMPLES',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_NEW_FROM_SAMPLES

SUBROUTINE IGRIB_CLONE(KHANDLE1,KHANDLE2)
INTEGER(KIND=JPIM),INTENT(IN)  :: KHANDLE1
INTEGER(KIND=JPIM),INTENT(OUT) :: KHANDLE2

INTEGER(KIND=JPIM) :: IRET
REAL(KIND=JPHOOK) :: ZHOOK_HANDLE

IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_CLONE',0,ZHOOK_HANDLE)
CALL GRIB_CLONE(KHANDLE1,KHANDLE2,STATUS=IRET)

IF(IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_CLONE FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT(' GRIB_CLONE FAILED')
ENDIF
IF (LHOOK) CALL DR_HOOK('GRIB_API:IGRIB_CLONE',1,ZHOOK_HANDLE)

END SUBROUTINE IGRIB_CLONE

SUBROUTINE IGRIB_IS_DEFINED(KHANDLE, CDKEY, LDDEFINED, KRET)
INTEGER(KIND=JPIM),           INTENT(IN)  :: KHANDLE
CHARACTER(LEN=*),             INTENT(IN)  :: CDKEY
LOGICAL,                      INTENT(OUT) :: LDDEFINED
INTEGER(KIND=JPIM), OPTIONAL, INTENT(OUT) :: KRET

INTEGER(KIND=JPIM) :: IS_DEFINED, IRET

IS_DEFINED = 0
#define FAGRIB2
#ifdef FAGRIB2
CALL GRIB_IS_DEFINED (KHANDLE, CDKEY, IS_DEFINED, IRET)
#else
CALL MPL_ABORT('GRIB_IS_DEFINED FAILED')
#endif

IF (PRESENT (KRET)) THEN
  KRET = IRET
ELSEIF (IRET /= JPGRIB_SUCCESS) THEN
  WRITE(0,*) 'GRIB_IS_DEFINED',KHANDLE,' ',CDKEY,' FAILED, RETURN CODE ',IRET
  CALL IGRIB_ERROR_MESSAGE(IRET)
  CALL MPL_ABORT('GRIB_IS_DEFINED FAILED')
ENDIF

LDDEFINED = IS_DEFINED /= 0

END SUBROUTINE IGRIB_IS_DEFINED

SUBROUTINE IGRIB_ERROR_MESSAGE(KRET)
INTEGER(KIND=JPIM),INTENT(IN) :: KRET
INTEGER(KIND=JPIM) :: IRET
CHARACTER(LEN=256) :: CLERRMSG

CLERRMSG = ''
CALL GRIB_GET_ERROR_STRING(KRET,CLERRMSG,STATUS=IRET)
WRITE(0,*) 'ECCODES ERROR MSG: ',TRIM(CLERRMSG)

END SUBROUTINE IGRIB_ERROR_MESSAGE

END MODULE YOWGRIB

