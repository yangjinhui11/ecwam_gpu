! (C) Copyright 1989- ECMWF.
! 
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.
!

SUBROUTINE SAVSTRESS(WVENVI, FF_NOW, NBLKS, NBLKE, CDTPRO, CDATEF)
! ----------------------------------------------------------------------

!*    PURPOSE.
!     --------
!     WRITE BINARY RESTART STRESS/WIND FIELDS TO DISK.

!**   INTERFACE.
!     ----------
!     *CALL* *SAVSTRESS(WVENVI, FF_NOW, NBLKS, NBLKE, CDTPRO, CDATEF)
!     *WVENVI*    WAVE ENVIRONMENT
!     *FF_NOW*    FORCING FIELDS AT CURRENT TIME
!     *NBLKS*     INDEX OF THE FIRST POINT OF THE SUB GRID DOMAIN.
!     *NBLKE*     INDEX OF THE LAST POINT OF THE SUB GRID DOMAIN.
!     *CDTPRO*    CHAR*14   END DATE OF PROPAGATION.
!     *CDATEF*    CHAR*14   START DATE FORECAST.

!     METHOD.
!     -------
!     IN CASE OF MESSAGE PASSING, THE OUTPUT IS DIRECTED STRAIGHT TO ITS
!     PLACE ON DISK, THE CORRESPONDING NAME AND DIRECTORY IS DETERMINED
!     BY GRSTNAME.
!     THE CONTRIBUTION FROM ALL PE'S NEED TO BE GATHERED ONTHE OUTPUT
!     PE BY CALLING MPGATHERSCFLD.

! ----------------------------------------------------------------------

      USE PARKIND_WAVE, ONLY : JWIM, JWRB, JWRU
      USE YOWDRVTYPE  , ONLY : ENVIRONMENT, FORCING_FIELDS

      USE YOWCOUT  , ONLY : NREAL    ,JPPFLAG  ,IPFGTBL, LRSTPARALW
      USE YOWGRID  , ONLY : IJSLOC   ,IJLLOC   ,IJGLOBAL_OFFSET,    &
 &                          NPROMA_WAM, NCHNK, KIJL4CHNK, IJFROMCHNK
      USE YOWMPP   , ONLY : IRANK    ,NPROC
      USE YOWPARAM , ONLY : NIBLO
      USE YOWTEST  , ONLY : IU06
      USE YOWTEXT  , ONLY : ICPLEN   ,CPATH
      USE YOWWIND  , ONLY : CDAWIFL  ,CDATEWO  ,CDATEFL

      USE YOMHOOK   ,ONLY : LHOOK,   DR_HOOK, JPHOOK

! ----------------------------------------------------------------------

      IMPLICIT NONE

#include "expand_string.intfb.h"
#include "grstname.intfb.h"
#include "mpgatherscfld.intfb.h"
#include "writestress.intfb.h"

      TYPE(ENVIRONMENT), INTENT(IN) :: WVENVI
      TYPE(FORCING_FIELDS), INTENT(IN) :: FF_NOW
      INTEGER(KIND=JWIM), DIMENSION(NPROC),INTENT(IN) :: NBLKS, NBLKE
      CHARACTER(LEN=14), INTENT(IN) :: CDTPRO, CDATEF


      INTEGER(KIND=JWIM) :: IFLD, IJ, IRECV
      INTEGER(KIND=JWIM) :: IJINF, IJSUP, MIJS, MIJL, IJOFFSET
      INTEGER(KIND=JWIM) :: IJSG, IJLG, ICHNK, KIJS, IJSB, KIJL, IJLB
      INTEGER(KIND=JWIM) :: LNAME
      INTEGER(KIND=JWIM) :: IFCST

      REAL(KIND=JPHOOK) :: ZHOOK_HANDLE
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RDUM 
      REAL(KIND=JWRB), ALLOCATABLE,DIMENSION(:,:) :: RFIELD

      CHARACTER(LEN=296) :: FILENAME

! ----------------------------------------------------------------------

IF (LHOOK) CALL DR_HOOK('SAVSTRESS',0,ZHOOK_HANDLE)

      IRECV=1

      IJSG = IJFROMCHNK(1,1)
      IJLG = IJSG + SUM(KIJL4CHNK) - 1

      IF (LRSTPARALW) THEN
        IJINF=IJSG
        IJSUP=IJLG
        MIJS=IJINF
        MIJL=IJSUP
        IJOFFSET=0
      ELSE
        IJINF=1
        IJSUP=NIBLO
        MIJS=IJSLOC
        MIJL=IJLLOC 
        IJOFFSET=IJGLOBAL_OFFSET
      ENDIF

      ALLOCATE(RFIELD(IJINF:IJSUP,NREAL))

!     ASSIGN THE DIFFERENT RESTART FIELDS TO OUTPUT ARRAY.
!     MAKE SURE IT IS THE SAME IN GETSTRESS !
!$OMP PARALLEL DO SCHEDULE(STATIC) PRIVATE(ICHNK, KIJS, IJSB, KIJL, IJLB)
      DO ICHNK = 1, NCHNK
        KIJS = 1
        IJSB = IJFROMCHNK(KIJS, ICHNK) + IJOFFSET
        KIJL = KIJL4CHNK(ICHNK)
        IJLB = IJFROMCHNK(KIJL, ICHNK) + IJOFFSET

        RFIELD(IJSB:IJLB,1)  = FF_NOW%WSWAVE(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,2)  = FF_NOW%WDWAVE(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,3)  = FF_NOW%UFRIC(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,4)  = FF_NOW%TAUW(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,5)  = FF_NOW%TAUWDIR(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,6)  = FF_NOW%Z0M(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,7)  = FF_NOW%Z0B(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,8)  = FF_NOW%CHRNCK(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,9)  = FF_NOW%AIRD(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,10) = FF_NOW%WSTAR(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,11) = FF_NOW%CICOVER(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,12) = FF_NOW%CITHICK(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,13) = WVENVI%UCUR(KIJS:KIJL,ICHNK)
        RFIELD(IJSB:IJLB,14) = WVENVI%VCUR(KIJS:KIJL,ICHNK)
      ENDDO
!$OMP END PARALLEL DO

      IF (.NOT.LRSTPARALW) THEN
!       COLLECT THE DIFFERENT CONTRIBUTIONS FROM THE PROCESSES
!       SINCE THE FILES ARE NOT SAVED IN SPLIT MODE
        DO IFLD=1,NREAL
          CALL MPGATHERSCFLD(IRECV,NBLKS,NBLKE, RFIELD(IJINF:IJSUP,IFLD),IJSUP-IJINF+1) 
        ENDDO
      ENDIF

!     SAVE RESTART
      IFCST = 0
      CALL GRSTNAME(CDTPRO,CDATEF,IFCST,'LAW',ICPLEN,CPATH,FILENAME)
      IF (LRSTPARALW) THEN
!        RESTART FILES FROM ALL PS's
         LNAME = LEN_TRIM(FILENAME)
         FILENAME=FILENAME(1:LNAME)//'.%p_%n'
         CALL EXPAND_STRING(IRANK,NPROC,0,0,FILENAME,1)
      ENDIF

      IF (LRSTPARALW .OR. IRANK == IRECV) THEN
          CALL WRITESTRESS(IJINF, IJSUP, NREAL, RFIELD, FILENAME, LRSTPARALW) 
      ENDIF
      DEALLOCATE(RFIELD)

      WRITE(IU06,*) ' '
      WRITE(IU06,*) 'SUB. SAVSTRESS: WIND FIELD SAVED FOR RESTART'
      WRITE(IU06,*) ' '
      WRITE(IU06,*) ' WIND FIELD WILL BE USED UNTIL .... CDATEWO = ', CDATEWO
      WRITE(IU06,*) ' NEXT WIND FILE NAME IS ........... CDAWIFL = ', CDAWIFL
      WRITE(IU06,*) ' NEXT WIND FILE WILL BE ACCESSED AT CDATEFL = ',CDATEFL
      CALL FLUSH(IU06)


IF (LHOOK) CALL DR_HOOK('SAVSTRESS',1,ZHOOK_HANDLE)

END SUBROUTINE SAVSTRESS
